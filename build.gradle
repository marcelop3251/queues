plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

group 'com.github.marcelop3251'
archivesBaseName = "queues-manager"
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    
    implementation 'com.amazonaws:aws-java-sdk-sqs:1.11.697'

    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.8'
    
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// Signing
apply plugin: 'signing'
signing {
    sign configurations.archives
}

apply plugin: 'maven'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            // Add required metadata to POM
            pom.project {
                name 'Queues Manager'
                packaging 'jar'
                description 'Convenience class for facilitating some common tasks with broker.'
                url 'https://github.com/marcelop3251/queues'

                scm {
                    connection 'scm:git:git://github.com/marcelop3251/queues.git'
                    developerConnection 'scm:git:ssh://github.com/marcelop3251/queues.git'
                    url 'http://github.com/marcelop3251/queues/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'marcelop3251'
                        name 'Marcelo Garcia Palma'
                        email 'marcelop3251@gmail.com'
                    }
                }
            }
        }
    }
}